program        → declaration* EOF

declaration    → varDecl
               | statement

varDecl        → "@SET" IDENTIFIER ( "=" expression )? ";"

statement      → exprStmt
               | block
               | ifStmt
               | forStmt

exprStmt       → expression ";"
block          → "{" declaration* "}"
ifStmt         → "if" "(" expression ")" statement ( "else" statement )?
forStmt        → "for" "(" ( varDecl | exprStmt | ";" )
                           expression? ";"
                           expression? ")" statement

expression     → assignment
assignment     → IDENTIFIER "=" assignment
               | equality
equality       → comparison ( ( "!=" | "==" ) comparison )*
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )*
term           → factor ( ( "-" | "+" ) factor )*
factor         → unary ( ( "/" | "*" ) unary )*
unary          → ( "!" | "-" ) unary
               | primary
primary        → NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")"
               | IDENTIFIER